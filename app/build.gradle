def versionMajor = 1
def versionMinor = 1
def versionPatch = 0
def versionBuild = {
    if (project.hasProperty("buildNumber")) {
        return project.buildNumber as int
    } else if (System.getenv("BUILD_NUMBER")) {
        return System.getenv("BUILD_NUMBER") as int
    } else {
        return 0
    }
}.call()

def buildTime = new Date().format("HH:mm:ss, dd/MM/yyyy", TimeZone.getTimeZone("UTC"))
def preDexEnabled = "true".equals(System.getProperty("pre-dex", "true"))
def projectName = "BTS"

// adjust variable below to detect your CI builds
def isCIBuild = System.getenv()['bamboo_planName'] ? true : false

Properties properties = new Properties()
properties.load(project.rootProject.file('keystores/bts.properties').newDataInputStream())

def restIp = properties.getProperty('rest.api.ip')
def restPort = properties.getProperty('rest.api.port')

/* signing configs */
def sf = properties.getProperty('store.file')
def sp = properties.getProperty('store.password')
def ka = properties.getProperty('key.alias')
def kp = properties.getProperty('key.password')

buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'com.neenbedankt.gradle.plugins:android-apt:1.4'
    }
}

apply plugin: 'com.android.application'
apply plugin: 'com.neenbedankt.android-apt'

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.2"

    signingConfigs {
        release {
            storeFile file("${sf}")
            storePassword "${sp}"
            keyAlias "${ka}"
            keyPassword "${kp}"
        }
    }

    defaultConfig {
        useLibrary 'org.apache.http.legacy'
        applicationId 'vn.vnpt.ansv.bts'
        minSdkVersion 19
        targetSdkVersion 23
        versionCode versionMajor * 10000000 + versionMinor * 100000 + versionPatch * 1000 + versionBuild
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"
        buildConfigField "String", "BUILD_TIME", "\"${buildTime}\""
        buildConfigField 'String', 'REST_API_IP', "\"${restIp}\""
        buildConfigField 'String', 'REST_API_PORT', "\"${restPort}\""
        buildConfigField 'String', 'VNPT_TECH_URL', "\"http://vnpt-technology.vn/HomeAction\""
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    dexOptions {
        preDexLibraries = preDexEnabled
    }

    packagingOptions {
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE-FIREBASE.txt'
        exclude 'META-INF/NOTICE'
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    // Rename the output apk
    applicationVariants.all { variant ->
        variant.outputs.each { output ->
            def outputFile = output.outputFile
            // don't use gitSha with dev builds because AS gets confused
            def filePrefix = isCIBuild ? "${projectName}-${variant.name}-${versionName}-${gitSha}" : "${projectName}-${variant.name}-${versionName}"
            if (outputFile != null && outputFile.name.endsWith('.apk')) {
                output.outputFile = file("$project.buildDir/${filePrefix}.apk")
            }
        }
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    // Android
    compile 'com.android.support:appcompat-v7:23.3.0'
    compile 'com.android.support:recyclerview-v7:23.3.0'
    compile 'com.android.support:cardview-v7:23.3.0'
    compile 'com.android.support:mediarouter-v7:23.3.0'
    compile 'com.android.support:palette-v7:23.3.0'
    compile 'com.android.support:design:23.3.0'
    // Butterknife
    compile 'com.jakewharton:butterknife:7.0.0'
    // Dagger 2
    compile 'com.google.dagger:dagger:2.0.1'
    apt 'com.google.dagger:dagger-compiler:2.0.1'
    provided 'org.glassfish:javax.annotation:10.0-b28'
    // RxJava, RxAndroid
    compile 'io.reactivex:rxjava:1.1.3'
    compile 'io.reactivex:rxandroid:1.1.0'
    // Gson
    compile 'com.google.code.gson:gson:2.4'
    // Retrofit
    compile 'com.squareup.retrofit:retrofit:1.9.0'
    compile 'com.squareup.okhttp:okhttp:2.5.0'
    compile 'com.android.volley:volley:1.0.0'
    // materialviewpager
    compile 'com.github.florent37:materialviewpager:1.2.3'
    compile 'com.github.kittinunf.fuel:fuel-android:1.11.0' //for Android

    // toasts
    compile 'com.github.johnpersano:supertoasts:1.3.4@aar'

}